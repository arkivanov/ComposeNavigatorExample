import org.jetbrains.compose.desktop.application.dsl.TargetFormat

plugins {
    id 'kotlin-multiplatform'
    id 'org.jetbrains.compose'
}

kotlin {
    jvm {
        withJava()
    }

    sourceSets {
        jvmMain {
            dependencies {
                implementation(project(":navigator"))
                implementation(project(":screens"))
                implementation(compose.desktop.currentOs)
                implementation("com.arkivanov.decompose:decompose:1.0.0-beta-01")
                implementation("com.arkivanov.decompose:extensions-compose-jetbrains:1.0.0-beta-01")
            }
        }
    }
}

compose {
    desktop {
        application {
            mainClass = "com.arkivanov.composenavigatorexample.desktop.MainKt"

            nativeDistributions {
                targetFormats = EnumSet.of(TargetFormat.Dmg, TargetFormat.Msi, TargetFormat.Deb)
                packageName = "ComposeNavigatorExample"
                packageVersion = "1.0.0"

                windows {
                    menuGroup = "ComposeNavigatorExample"
                    // see https://wixtoolset.org/documentation/manual/v3/howtos/general/generate_guids.html
                    upgradeUuid = "E6C543A2-2C39-4534-8BA0-958BE83F4875"
                }
            }
        }
    }
}
